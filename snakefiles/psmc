import shutil
import os

os.environ["PSMC_PATH"] = os.path.join(config['basedir'], "lib", "psmc", "psmc")

rule bcf_to_psmcfa:
    input:
        bcf="{analysis}/chr{chrom}.bcf",
        csi="{analysis}/chr{chrom}.bcf.csi"
    output:
        r"{analysis}/chr{chrom}.sample{i,\d+}.psmcfa.gz"
    script:
        "../scripts/psmcfa.py"

rule combine_psmcfa:
    input: 
        "{analysis}/psmc/params.pkl"
    output: 
        temporary("{analysis}/psmc/combined.psmcfa.gz")
    run:
        params = load_file(input[0])
        with open(output[0], "wb") as f:
            for fn in params['input_files']:
                with open(fn, "rb") as fin:
                    shutil.copyfileobj(fin, f)

rule psmc_estimate:
    input:
        ["{analysis}/psmc/params.pkl", r"{analysis}/psmc/combined.psmcfa.gz"]
    output:
        ["{analysis}/psmc/estimates.pkl"]
    params:
        args=["-p", "64*1"],
        mutation_rate=lambda wc, input: load_file(input[0])['mutation_rate']
    resources:
        runtime=1440,
    script:
        "../scripts/psmc.py"
